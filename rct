###Abstract 
**Title**: Physical Activity Intervention for obese men above the age of fifty years. 
**Authors**: R. Thiesmeier, N. Orsini 
**Background**: Abdominal obesity in middle-age and elderly men is associated with higher incidence of chronic diseases. No previous studies evaluated the effect of a physical activity group intervention with professional coaching in reducing body weight. The aim is to evaluate using the effectiveness of a physical group activity intervention. 
**Methods**: We conducted a trial including 170 individuals randomly allocated to either control (76) and intervention (94). A linear regression model was used to conduct statistical inference (95% confidence interval (CI) and t-test) on the effect of the intervention on the mean body weight. 
**Results**: The estimated mean difference in the intervention population was -2.17 kg lower than the control population (95% CI = -5.23, 0.95). A t-test indicated compatibility between this data and the hypothesis of no intervention effect on the mean outcome (t = -1.37, p = 0.174).
**Conclusions**: This study provided only a suggestion of possible beneficial effect of physical activity group intervention program to reduce body weight for obese men over the age of fifty years.
**Keywords**: Physical activity; Intervention; Obesity; Men

###Stata code
// sample size calculation 
power twomeans 120 115, sd(10) n(170)

// Generate trial data 
clear all 
set obs 170
set seed 20240224
gen pa = rbinomial(1, .5)
gen bmi = rnormal(120-5*pa, 10)
regress bmi pa 

capture program drop sim_lm
program define sim_lm, rclass
	drop _all
	set obs 170
	gen pa = rbinomial(1, .5)
	gen bmi = rnormal(120-5*pa, 10)
	reg bmi pa 
	return scalar est_b = _b[pa]
	return scalar est_se_b = _se[pa]
	test _b[pa] = 0
	return scalar p = r(p)
end

// Generate one study

sim_lm 

// Generate 1,000 studies

simulate est_b  = r(est_b) est_se_b = r(est_se_b) p =r(p), reps(1000): sim_lm

// describe the sampling distribution of the estimated effect
summarize 

// simulated power
count if p < 0.05
di "Simulated Power (%) = " r(N)/c(N)*100 
